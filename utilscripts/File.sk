
# @name File_preventBacktracking
# @description Prevents backtracking in a file path
#
# @authors zNotChill
# @param {string} path - The path to prevent backtracking in
# @returns {string} - The path without backtracking
# @example File_preventBacktracking("/.././test") -> "./test"

function File_preventBacktracking(path: string) :: string:
  if {_path} contains "..":
    replace all ".." in {_path} with ""
  while {_path} starts with "/":
    replace first "/" in {_path} with ""
  return {_path}

# @name File_formatFileList
# @description Formats a file list
#
# @authors zNotChill
# @param {string} fileList - The file list to format
# @returns {strings} - The formatted file list
# @dependencies java.nio.file.DirectoryStream, java.nio.file.Files, java.util.ArrayList
# @example File_formatFileList("[plugins\Skript.jar, plugins\skript-reflect.jar]") -> ["plugins\Skript.jar", "plugins\skript-reflect.jar"]

function File_formatFileList(fileList: string) :: strings:
  set {_files::*} to split {_fileList} by ","
  set {_formattedFiles::*} to new ArrayList()

  loop {_files::*}:
    set {_file} to loop-value
    replace all "[" in {_file} with ""
    replace all "]" in {_file} with ""
    replace first " " in {_file} with ""
    add {_file} to {_formattedFiles::*}

  return {_formattedFiles::*}

# @name File_getFilesInDir
# @description Gets the files in a directory
#
# @authors zNotChill
# @param {string} path - The path of the directory
# @returns {strings} - The files in the directory
# @dependencies java.nio.file.DirectoryStream, java.nio.file.Files, java.io.File
# @example File_getFilesInDir("plugins") -> ["plugins\Skript.jar", "plugins\skript-reflect.jar"]

function File_getFilesInDir(path: string) :: strings:
  set {_file} to new File({_path})
  set {_files} to {_file}.listFiles()
  set {_files::*} to File_formatFileList("%{_files}%")

  loop {_files::*}:
    add loop-value to {_formattedFiles::*}

  return List_removeEmptyEntries({_files::*})

# @name File_recGetFilesInDir
# @description Recursively gets the files in a directory
#
# @authors zNotChill
# @param {string} path - The path of the directory
# @returns {strings} - The files in the directory
# @dependencies java.nio.file.DirectoryStream, java.nio.file.Files, java.io.File
# @example File_recGetFilesInDir("plugins") -> ["plugins\Example.jar", "plugins\Example2.jar", "plugins\Example\config.yml"]

function File_recGetFilesInDir(path: string) :: strings:
  set {_file} to new File({_path})
  set {_files} to {_file}.listFiles()
  set {_files::*} to File_formatFileList("%{_files}%")

  loop {_files::*}:
    add 1 to {_i}

    if {_i} > 100:
      return List_removeEmptyEntries({_formattedFiles::*})

    set {_fileCast} to new File(loop-value)
    if {_fileCast}.isFile():
      add loop-value to {_formattedFiles::*}
    else:
      set {_subFiles::*} to File_recGetFilesInDir(loop-value)
      add {_subFiles::*} to {_formattedFiles::*}

  return List_removeEmptyEntries({_formattedFiles::*})

# @name File_getRelativePath
# @description Gets the relative path of a file
#
# @authors zNotChill
# @param {string} path - The path of the file
# @returns {string} - The relative path of the file
# @example File_getRelativePath("plugins") -> "plugins"

function File_getRelativePath(path: string) :: string:
  replace Server_getPath() in {_path} with ""
  return {_path}

# @name File_getRelativeFileContent
# @description Gets the content of a file relative to the server path
#
# @authors zNotChill
# @param {string} relative_path - The relative path of the file
# @returns {string} - The content of the file
#
# @dependencies java.nio.file.Files, java.nio.file.Paths
# @example File_getRelativeFileContent("eula.txt") -> "eula=true"

function File_getRelativeFileContent(relative_path: string) :: object:
  set {_path} to try Paths.get(File_preventBacktracking(Server_getPath() + "\\%{_relative_path}%"))
  set {_content} to try Files.readString({_path})
  if {_content} is not set:
    return false
  return {_content}

# @name File_fixPath
# @description Fixes slashes in a file path
#
# @authors zNotChill
# @param {string} path - The path to fix slashes in
# @returns {string} - The path with fixed slashes
# @example File_fixPath("/plugins/") -> "\\plugins\\"

function File_fixPath(path: string) :: string:
  replace all "\" in {_path} with "\\"
  replace all "/" in {_path} with "\\"
  return {_path}

# @name File_fixBackslashes
# @description Fixes backslashes in a file path
#
# @authors zNotChill
# @param {string} path - The path to fix backslashes in
# @returns {string} - The path with fixed backslashes
# @example File_fixBackslashes("\\plugins\\") -> "/plugins/"

function File_fixBackslashes(path: string) :: string:
  replace all "\\" in {_path} with "/"
  return {_path}